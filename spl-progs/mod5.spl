//scheduler

multipush(BP);
alias curPID R0;
curPID = [SYSTEM_STATUS_TABLE + 1];
//backup other registers to process_table
alias process_table R1;
process_table = PROCESS_TABLE + 16*curPID;

[process_table + 12] = SP%512;
[process_table + 14] = PTBR;
[process_table + 15] = PTLR;

alias newPID R2;
newPID = curPID+1;
alias next_process_table R3;

if [SYSTEM_STATUS_TABLE + 5] != 0 then
    //if swapper is in blocked state schedule idle process
    if [PROCESS_TABLE + 15*16 + 4] == WAIT_DISK then 
        newPID = 0;
    else
    //else swapper daemon will be scheduled
        newPID = 15;
    endif;
else 
    while 1 == 1 do 
        if [PROCESS_TABLE + 16*newPID + 6] != 1 then
            if [PROCESS_TABLE + 16*newPID + 4] == READY || [PROCESS_TABLE + 16*newPID + 4] == CREATED then
                break;
            endif;
        endif;
        newPID = (newPID + 1)%16;
    endwhile;
endif;


next_process_table = PROCESS_TABLE + 16*newPID;
//restore the registers from new process
PTBR = [next_process_table + 14];
PTLR = [next_process_table + 15];

[SYSTEM_STATUS_TABLE + 1] = newPID;

//if the new process is not schedule before, then switch to userstack
if([next_process_table + 4] == CREATED) then
    //get bp from first word of kernel stack
    BP = [[next_process_table + 11]*512];
    SP = [next_process_table + 13];
    [next_process_table + 4] = RUNNING;
    [next_process_table + 9] = 0;
    [next_process_table + 1] = newPID;
    ireturn;
endif;


[next_process_table + 4] = RUNNING;
SP = [next_process_table + 11]*512 + [next_process_table + 12];
multipop(BP);
return;
